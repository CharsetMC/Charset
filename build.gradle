buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
		classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = "0.4.2"
group = "pl.asie.charset"
archivesBaseName = "Charset"

if (System.getenv("BUILD_NUMBER") != null)
	version += ".${System.getenv("BUILD_NUMBER")}"

configurations {
	deploy
}

dependencies {
	deploy 'org.apache.maven.wagon:wagon-ssh:2.10'
}

minecraft {
	version = "1.12-14.21.1.2426"

	runDir = "run"
	useDepAts = true
	makeObfSourceJar = false
	mappings = "snapshot_20170724"

	replace '@VERSION@', project.version
	replace '@MC_VERSION@', version
}

repositories {
	mavenCentral()
	maven { url "http://maven.amadornes.com/" }
	maven {
		name 'DVS1 Maven FS'
		url 'http://dvs1.progwml6.com/files/maven'
	}
	maven {
		name 'Sonatype OSSRH (Snapshots)'
		url 'https://oss.sonatype.org/content/repositories/snapshots/'
	}
	maven {
		name "Mighty Pirates"
		url "http://maven.cil.li"
	}
	maven {
		name "Cyclops Repo"
		url "https://dl.bintray.com/cyclopsmc/dev/"
	}
	maven {
		url "http://repo.elytradev.com"
	}
	ivy {
		name 'asie dependency mirror'
		artifactPattern "http://asie.pl/javadeps/[module]-[revision](-[classifier]).[ext]"
	}
}

configurations {
	shadow
	compile.extendsFrom shadow
}

dependencies {
	deobfCompile "MCMultiPart2:MCMultiPart-exp:2.2.2_38"
	deobfCompile "mezz.jei:jei_1.12:4.7.2.77"
	deobfCompile "org.cyclops.commoncapabilities:CommonCapabilities:1.12-1.3.4-132"
	deobfCompile name: "Baubles", version: "1.12-1.4.5"
	deobfCompile "elucent:albedo:2.0-SNAPSHOT"
	deobfCompile "li.cil.oc:OpenComputers:MC1.12-1.7.+:api"
	shadow name: "libresample4j", version: "bc0a030"
}

sourceSets {
	main {
		resources {
			srcDirs += 'lang'
			srcDirs += 'docs'
		}
	}
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// replace version and mcversion
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

jar.dependsOn shadowJar
jar.enabled = false

shadowJar {
	archiveName = tasks.jar.archiveName
	configurations = [project.configurations.shadow]
	relocate 'com.laszlosystems.libresample4j', 'pl.asie.charset.module.audio.libresample4j'
}

reobfJar {
	setJar(tasks.shadowJar.archivePath)
}
reobfJar.dependsOn shadowJar

task apiJar(type: Jar) {
	classifier = "api"
	from(sourceSets.main.output) {
		include "pl/asie/charset/api/**"
		include "licenses/**"
	}
}

artifacts {
	archives apiJar
}

if (file('private.gradle').exists()) {
	apply from: 'private.gradle'
}
